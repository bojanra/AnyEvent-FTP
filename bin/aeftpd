#!/usr/bin/perl

use strict;
use warnings;
use v5.10;
use AnyEvent;
use AnyEvent::FTP::Server;
use Getopt::Long qw( GetOptions );
use URI;
use Pod::Usage qw( pod2usage );

# PODNAME: aeftpd
# ABSTRACT: FTP Server
# VERSION

=head1 SYNOPSIS

 % aeftpd [ --port port ] \
          [ --hostname hostname ] \
          [ --inet ] \
          [ --stderr path ] \
          [ --chroot ] \
          [ --pam service | --random_auth | --auth user:pass ] \
          [ --verbose ]
 % aeftpd --version
 % aeftpd --help

=head1 DESCRIPTION

This program starts an FTP daemon using the perl library L<AnyEvent::FTP::Server>.

=head1 OPTIONS

=head2 --port I<port>

The TCP port to listen to.

=head2 --hostname I<hostname>

The hostname or IP address to listen on.

=head2 --inet

Run in inet mode.  By default C<aeftpd> runs as a single process in standalone mode.
By using this option you can run C<aeftp> from C<inetd>, C<xinetd> or similar daemon.
Here is a line for /etc/inetd.conf which was tested on Debian Wheezy, and may work
for you if your operating system suppors PAM and you have L<Authen::Simple::PAM>
installed.

 ftp stream tcp nowait root /usr/sbin/tcpd aeftpd --inet --stderr /tmp/aeftp.log --pam login

=head2 --stderr I<path>

Redirect stderr from the daemon to the file specified by the given path

=head2 --chroot

Use C<chroot> to restrict the user to only his home directory once he has logged
in.  This option requires the C<chroot> function, which is supported by Perl on
most UNIX and UNIX like operating systems.

=head2 --random_auth

Generate a random username and password for authentication.

=head2 --auth I<user>:I<pass>

Allow authentication with the given username and password.

=head2 --verbose

Print all FTP commands and their responses to stderr.

=head2 --version

Print out the L<AnyEvent::FTP> version to stdout and exit.

=head2 --help

Display the usage for this command.

=cut

my $port;
my $host;
my $inet = 0;
my $stderr;
my $pam;
my $chroot = 0;
my $verbose = 0;
my $random_auth;
my $auth;

GetOptions(
  'port=i'      => \$port,
  'hostname=s'  => \$host,
  'inet'        => \$inet,
  'stderr=s'    => \$stderr,
  'pam=s'       => \$pam,
  'chroot'      => \$chroot,
  'random_auth' => \$random_auth,
  'verbose'     => \$verbose,
  'auth=s'      => \$auth,
  'help|h'      => sub { pod2usage({ -verbose => 2}) },
  'version'     => sub { say 'aeftp/AnyEvent::FTP version ', ($AnyEvent::FTP::Server::VERSION // 'dev'); exit 1 },
) || pod2usage(1);

$0 = 'aeftpd';

$port //= ($> && $^O !~ /^(cygwin|MSWin32)$/) ? 8021 : 21;

if($stderr)
{
  open STDERR, '>>', $stderr;
}

if(defined $auth)
{
  my($user,$pass) = split /:/, $auth;
  $auth = {
    user => $user,
    pass => $pass,
  };
}
elsif(defined $random_auth)
{
  $auth = {
    user => (join '', map { chr(ord('a') + int rand(26)) } (1..10)),
    pass => (join '', map { chr(ord('a') + int rand(26)) } (1..10)),
  };
}

unless($inet)
{
  my $uri = URI->new('ftp:');
  $uri->host($host // 'localhost');
  $uri->port($port);
  $uri->userinfo(join ':', $auth->{user}, $auth->{pass})
    if defined $auth;
  say $uri;
}

my $server = AnyEvent::FTP::Server->new(
  hostname => $host,
  port     => $port,
  inet     => $inet,
);

if($verbose)
{
  $server->on_connect(sub {
    my $con = shift;

    $con->on_request(sub {
      my $raw = shift;
      say STDERR "CLIENT: $raw";
    });
  
    $con->on_response(sub {
      my $raw = shift;
      $raw =~ s/\015?\012$//g;
      say STDERR "SERVER: $raw";
    });
    
    $con->on_close(sub {
      say STDERR "DISCONNECT";
    });
    
    say STDERR "CONNECT";
  });
}

if($auth)
{
  $server->on_connect(sub {
    my $con = shift;
    $con->context->authenticator(sub {
      my($name, $pass) = @_;
      return $name eq $auth->{user}
      &&     $pass eq $auth->{pass};
    });
  });
}
elsif($pam)
{
  eval { require Authen::Simple::PAM };
  if($@)
  {
    say STDERR "PAM authentication requires Authen::Simple::PAM to be installed";
    exit 2;
  }
  
  $pam = Authen::Simple::PAM->new(
    service => $pam,
  );
  
  $server->on_connect(sub {
    my $con = shift;
    
    my $user_class;
    if($inet && $< == 0 && $^O !~ /^(cygwin|MSWin32)$/)
    {
      $user_class = 'AnyEvent::FTP::Server::OS::UNIX';
      eval "use $user_class"; die $@ if $@;
    }
    
    $con->context->authenticator(sub {
      my($name, $pass) = @_;
      
      $name = 'ftp' if $name eq 'anonymous';
     
      my $user;
      if(defined $user_class)
      {
        $user = eval { $user_class->new($name) };
        return 0 if $@;
      }

      return 0 if $name ne 'ftp' && ! $pam->authenticate( $name, $pass );
      
      if(defined $user)
      {
        $user->jail if $chroot || $name eq 'ftp';
        $user->drop_privileges;
      }
    });
    
    $con->context->bad_authentication_delay(0);
      
    1;
    
  });
}

$server->start;

AnyEvent->condvar->recv;
